buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:${dependencyManagementVersion}"
    }
}

subprojects {
    group = 'com.js'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = '11'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
        implementation 'org.springframework.boot:spring-boot-starter-webflux'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'io.projectreactor:reactor-test'
    }

    test {
        useJUnitPlatform()
    }
}

def dataProjects = subprojects.findAll { it.name != 'ch1-reactive' && it.name != 'ch8-reactive-client' }

configure(dataProjects) {
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
        implementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:3.2.4'
        implementation 'org.mongodb:mongodb-driver-sync:4.4.0'
    }
}
